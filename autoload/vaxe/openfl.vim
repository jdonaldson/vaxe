function! vaxe#openfl#Targets(...)
    return s:openfl_targets
endfunction

function! vaxe#openfl#Update(...)
    if (a:0 && a:1 != '')
        let target = split(a:1)[0]
    else
        let target = g:vaxe_openfl_target
    endif
    let command = "openfl update ".target
    call s:Log(command)
    call s:Sys(command)
endfunction

function! vaxe#openfl#ProjectOpenfl(...)
    if exists('g:vaxe_openfl')
        unlet g:vaxe_openfl
    endif
    let g:vaxe_working_directory = getcwd()

    if a:0 > 0 && a:1 != ''
        let g:vaxe_openfl = expand(a:1,':p')
    else
        let openfls = split(glob("**/*.xml"),'\n')

        if len(openfls) == 0
            echoerr "No openfl files found in current working directory"
            return
        else
            let base_openfl = vaxe#util#InputList("Select Openfl", openfls)
        endif

        if base_openfl !~ "^//"
            let base_openfl = getcwd() . '/' . base_openfl
        endif

        let g:vaxe_openfl = base_openfl
    endif
    if !filereadable(g:vaxe_openfl)
        echoerr "Project openfl file not valid, please create one."
        return
    endif
    call vaxe#SetCompiler()
    return g:vaxe_openfl
endfunction

function! vaxe#openfl#Clean(...)
    if (a:0 && a:1 != '')
        let target = split(a:1)[0]
    else
        let target = g:vaxe_openfl_target
    endif
    let command = "openfl clean ".target
    call s:Log(command)
    call s:Sys(command)
endfunction

"A simple system function that first changes directory to the current vaxe
"working directory
function! s:Sys(cmd)
    call system("cd ".g:vaxe_working_directory." && ".a:cmd)
endfunction

function! vaxe#openfl#BuildOpenflHxml()
    let base_hxml = b:vaxe_openfl.".hxml"

    if !strlen(g:vaxe_openfl_target)
        call vaxe#openfl#Target()
    endif

    let g:vaxe_working_directory = fnamemodify(b:vaxe_openfl, ":p:h")
    let cdcmd = 'cd "'.g:vaxe_working_directory.'" && '

    "create the openfl.hxml if not present
    if !filereadable(base_hxml)
        " pipe openfl display to an hxml for completions
        let escape_base = fnameescape(base_hxml)
        call s:Sys(" echo '# THIS FILE IS AUTOGENERATED BY VAXE, ANY EDITS ARE DISCARDED' " . " > " . escape_base)
        call s:Sys(" openfl display " . g:vaxe_openfl_target
                    \. " >> " . escape_base )
    endif

    " create the boilerplate code if missing
    let simple_target = split(g:vaxe_openfl_target)[0]
    if (!isdirectory(g:vaxe_working_directory."/bin/".simple_target))
        " build the assets dependencies
        call system(cdcmd . " openfl update " . g:vaxe_openfl_target)
    else
    endif

    let g:vaxe_openfl = b:vaxe_openfl
    let b:vaxe_hxml = base_hxml
    " let g:vaxe_hxml = b:vaxe_hxml " don't set a global projet var by default
endfunction

"Sets the target.  If target is missing it asks the user. Also updates the
"makeprg compiler command
function! vaxe#openfl#Target(...)
    let g:vaxe_openfl_target = ''
    if a:0 > 0 && a:1 != ''
        let g:vaxe_openfl_target = a:1
    else
        let g:vaxe_openfl_target = vaxe#util#InputList("Select Openfl Target", s:openfl_targets)
        let g:vaxe_openfl_target = split(g:vaxe_openfl_target, ":")[0]
    endif
    call vaxe#openfl#BuildOpenflHxml()
    call vaxe#SetCompiler()
endfunction


" A list of all the openfl targets
let s:openfl_targets = [ "android : Create Google Android applications"
            \, "android -arm7 : Compile for arm-7a and arm5"
            \, "android -arm7-only : Compile for arm-7a for testing"
            \, "blackberry : Create BlackBerry applications"
            \, "blackberry -simulator : Build/test for the device simulator"
            \, "flash : Create SWF applications for Adobe Flash Player"
            \, "html5 : Create HTML5 canvas applications"
            \, "html5 -minify : Minify output using the Google Closure compiler"
            \, "html5 -minify -yui : Minify output using the YUI compressor"
            \, "ios : Create Apple iOS applications"
            \, "ios -simulator : Build/test for the device simulator"
            \, "ios -simulator -ipad : Build/test for the iPad Simulator"
            \, "linux : Create Linux applications"
            \, "linux -64 : Compile for 64-bit instead of 32-bit"
            \, "linux -neko : Build with Neko instead of C++"
            \, "linux -neko -64 : Build with Neko 64-bit instead of C++"
            \, "mac : Create Apple Mac OS X applications"
            \, "mac -neko : Build with Neko instead of C++"
            \, "mac -neko -64 : Build with Neko 64-bit instead of C++"
            \, "webos : Create HP webOS applications"
            \, "windows : Create Microsoft Windows applications"
            \, "windows -neko : Build with Neko instead of C++"
            \, "windows -neko -64 : Build with Neko 64-bit instead of C++" ]

  " -D : Specify a define to use when processing other commands
  " -debug : Use debug configuration instead of release
  " -verbose : Print additional information (when available)
  " -clean : Add a "clean" action before running the current command
  " (display) -hxml : Print HXML information for the project
  " (display) -openfl : Print openfl information for the project
